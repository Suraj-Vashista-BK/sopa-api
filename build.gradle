apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'jacoco'
apply plugin: 'findbugs'

apply plugin: 'checkstyle'
checkstyle {
    toolVersion '7.8.1'
    configFile file("$rootProject.projectDir/config/checkstyle/checkstyle.xml")
}

checkstyleMain {
    source ='src/main/java'
}

checkstyleTest {
    source = []
}

group 'io.github.kwahome.sopa'
version '0.4.0'
archivesBaseName = "sopa-api"
description = """Configurable and extensible structured logging on top of `slf4j` API to generate easily parsable log messages in desired formats."""
sourceCompatibility = 1.8
targetCompatibility = 1.8

// Dependency versions
// ---------------------------------------
def $junitVersion = "4.12"
def $slf4jVersion = "1.7.25"
def $javaxJsonVersion = "1.1.3"
def $lombokVersion = "1.18.4"
def $lidaliaVersion = "1.2.0"
def $hamcrestVersion = "1.3"
def $guavaVersion = "23.0"
// --------------------------------------

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.glassfish', name: 'javax.json', version: $javaxJsonVersion
    compile 'com.google.guava:guava:' + $guavaVersion
    compileOnly "org.projectlombok:lombok:" + $lombokVersion
    compile group: 'org.slf4j', name: 'slf4j-api', version: $slf4jVersion

    testCompile group: 'junit', name: 'junit', version: $junitVersion
    testCompile group: 'uk.org.lidalia', name: 'slf4j-test', version: $lidaliaVersion
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: $hamcrestVersion
    testCompileOnly "org.projectlombok:lombok:" + $lombokVersion
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Implementation-Version': project.version
        )
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport

test {
    finalizedBy jacocoTestReport
    testLogging.showStandardStreams = true
    testLogging {
        events "passed", "skipped", "failed"
    }
}

// code quality
findbugs {
    effort = "max"
    reportLevel = "medium"
    includeFilter = file("$rootProject.projectDir/config/findbugs/findBugsIncludeFilter.xml")
    excludeFilter = file("$rootProject.projectDir/config/findbugs/findBugsExcludeFilter.xml")
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

build.dependsOn check

// ensure code quality for published artifacts
// install.dependsOn check

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing{
    publications{
        mavenJava(MavenPublication){
            artifactId = 'sopa-api'
            from components.java
            artifact sourceJar
            artifact javadocJar
            pom{
                name = 'sopa-api'
                description ='Configurable and extensible structured logging on top of `slf4j` API to generate easily parsable log messages in desired formats.'
                url ='https://github.com/kwahome/sopa-api'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/kwahome/sopa-api/blob/master/LICENSE'
                        distribution = 'repo'
                    }
                }
                scm {
                    url = 'https://github.com/kwahome/sopa-api'
                    connection = 'scm:git:git://github.com/kwahome/sopa-api.git'
                    developerConnection = 'scm:git:ssh://git@github.com:kwahome/sopa-api.git'
                }
                developers {
                    developer {
                        id = 'kwahome'
                        name = 'Kelvin Wahome'
                        email = 'kevowahome@gmail.com'
                    }
                }
            }
        }
    }
//    repositories {
//        maven {
//            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
//            credentials {
//                username = ossrhUsername
//                password = ossrhPassword
//            }
//        }
//    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html4', true)
    }
}